plugins {
    id "com.android.application"
    id "org.jetbrains.kotlin.android"
    id "kotlin-kapt"
    id "androidx.navigation.safeargs.kotlin"
    id "com.google.android.libraries.mapsplatform.secrets-gradle-plugin"
    id "org.jlleitschuh.gradle.ktlint"
}

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

def redirectPropertiesFile = rootProject.file("local.properties")
def redirectProperties = new Properties()
redirectProperties.load(new FileInputStream(redirectPropertiesFile))

android {
    namespace = "com.avangarde.polis"
    compileSdk 33
    defaultConfig {
        applicationId "com.avangarde.polis"
        minSdk 27
        //noinspection OldTargetApi
        targetSdk 32
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties["keyAlias"]
            keyPassword keystoreProperties["keyPassword"]
            storeFile file(keystoreProperties["storeFile"])
            storePassword keystoreProperties["storePassword"]
        }
    }

    buildFeatures {
        viewBinding true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
}

ktlint{
    android=true
    ignoreFailures=true
    reporters{
        reporter "plain"
        reporter "checkstyle"
        reporter "sarif"
    }
}

ext{
    lifecycle = "2.5.1"
    nav = "2.5.3"
    kotlinx_coroutines = "1.6.4"
    dagger2 = "2.44.2"
    retrofit = "2.9.0"
    moshi = "1.14.0"
    truth = "1.1.3"
    mockk = "1.12.4"
}

dependencies {
    //General
    implementation "androidx.core:core-ktx:1.9.0"
    implementation "androidx.appcompat:appcompat:1.5.1"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlinx_coroutines"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinx_coroutines"
    //UI
    implementation "com.google.android.material:material:1.7.0"
    implementation "androidx.constraintlayout:constraintlayout:2.1.4"
    implementation "androidx.activity:activity-ktx:1.6.1"
    implementation "androidx.fragment:fragment-ktx:1.5.5"
    implementation "androidx.core:core-splashscreen:1.0.0"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle"
    implementation "androidx.navigation:navigation-fragment-ktx:$nav"
    implementation "androidx.navigation:navigation-ui-ktx:$nav"
    //DI
    implementation "com.google.dagger:dagger:$dagger2"
    kapt "com.google.dagger:dagger-compiler:$dagger2"
    //ApiData
    implementation "com.google.android.libraries.places:places:3.0.0"
    //LocalData
    implementation "androidx.datastore:datastore-preferences:1.1.0-dev01"
    //Network
    implementation "com.squareup.retrofit2:retrofit:$retrofit"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit"
    implementation "com.squareup.moshi:moshi-kotlin:$moshi"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$moshi"
    //LoginFlow
    implementation "net.openid:appauth:0.11.1"
    implementation "com.auth0.android:jwtdecode:2.0.1"
    //Testing
    testImplementation "junit:junit:4.13.2"
    testImplementation "io.mockk:mockk:$mockk"
    testImplementation "com.google.truth:truth:$truth"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$kotlinx_coroutines"
    androidTestImplementation "com.google.truth:truth:$truth"
    androidTestImplementation "io.mockk:mockk:$mockk"
    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$kotlinx_coroutines"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.5.0"
    androidTestImplementation "androidx.test.ext:junit:1.1.4"
    androidTestImplementation "androidx.navigation:navigation-testing:$nav"

    //Util
    implementation "com.jakewharton.timber:timber:5.0.1"
    implementation "com.facebook.fresco:fresco:2.6.0"
}